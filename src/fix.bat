@echo off

rem Sets up the qtetris package for building with the specified compiler,
rem and if possible converting text files from LF to CR/LF format.

if [%2] == [--quick] set AL_NOCONV=1

if [%1] == [bcc32]   goto bcc32
if [%1] == [djgpp]   goto djgpp
if [%1] == [mingw32] goto mingw32
if [%1] == [msvc]    goto msvc
if [%1] == [rsxnt]   goto rsxnt
if [%1] == [watcom]  goto watcom

if [%1] == [help]    goto help
if [%1] == []        goto help

goto done

:bcc32

set AL_COMPILER=Windows (BCC32)
set AL_MAKEFILE=makefile.bcc
set AL_PLATFORM=ALLEGRO_BCC32
set AL_NOCONV=1
goto fix

:djgpp

set AL_COMPILER=DOS (djgpp)
set AL_MAKEFILE=makefile.dj
set AL_PLATFORM=ALLEGRO_DJGPP
goto fix

:mingw32

set AL_COMPILER=Windows (Mingw32)
set AL_MAKEFILE=makefile.mgw
set AL_PLATFORM=ALLEGRO_MINGW32
set AL_NOCONV=1
goto fix

:msvc

set AL_COMPILER=Windows (MSVC)
set AL_MAKEFILE=makefile.vc
set AL_PLATFORM=ALLEGRO_MSVC
goto fix

:rsxnt

set AL_COMPILER=Windows (RSXNT)
set AL_MAKEFILE=makefile.rsx
set AL_PLATFORM=ALLEGRO_RSXNT
goto fix

:watcom

set AL_COMPILER=DOS (Watcom)
set AL_MAKEFILE=makefile.wat
set AL_PLATFORM=ALLEGRO_WATCOM
goto fix

:help

echo.
echo Usage: fix platform [--quick]
echo.
echo Where platform is one of: bcc32, djgpp, dll, mingw32, msvc, rsxnt or watcom.
echo The --quick parameter is used to turn off LF to CR/LF conversion.
echo.
goto done

:fix

echo Configuring Allegro for %AL_COMPILER%...

echo # generated by fix.bat > makefile
echo MAKEFILE_INC = %AL_MAKEFILE% >> makefile
echo include makefile.all >> makefile

echo /* generated by fix.bat */ > include\allegro\alplatf.h
echo #define %AL_PLATFORM% >> include\allegro\alplatf.h

if [%AL_NOCONV%] == [1] goto done

echo Converting Allegro files to %AL_COMPILER% format...
utod *.bat .../*.c *.cfg .../*.h .../*.inc .../*.rc
utod .../*.s .../*.txt .../*._tx makefile.* readme.*

:done

set AL_COMPILER=
set AL_PLATFORM=
set AL_MAKEFILE=
set AL_NOCONV=

echo Done!
